;;;; -*- coding:utf-8 -*-
;;;;****************************************************************************
;;;;FILE:               peek-stream.lisp
;;;;LANGUAGE:           Common-Lisp
;;;;SYSTEM:             Common-Lisp
;;;;USER-INTERFACE:     NONE
;;;;DESCRIPTION
;;;;    
;;;;    This package exports a class named PEEK-STREAM that encapsulates
;;;;    a stream and a buffer in such a way that reading, peeking or
;;;;    unreading characters can be done in any number and in any order.
;;;;    
;;;;    We don't use gray stream to keep it pure Common-Lisp.
;;;;    The I/O methods are GETCHAR, UNGETCHAR and NEXTCHAR to avoid
;;;;    name clashes with un-generic READ-CHAR, UNREAD-CHAR and PEEK-CHAR.
;;;;    
;;;;AUTHORS
;;;;    <PJB> Pascal J. Bourguignon <pjb@informatimago.com>
;;;;MODIFICATIONS
;;;;    2004-09-06 <PJB> Extracted from parse-html.
;;;;BUGS
;;;;    Does not implement other I/O than these three character input methods.
;;;;    NEXTCHAR can be called only once in a row.
;;;;LEGAL
;;;;    GPL
;;;;    
;;;;    Copyright Pascal J. Bourguignon 2004 - 2004
;;;;    
;;;;    This program is free software; you can redistribute it and/or
;;;;    modify it under the terms of the GNU General Public License
;;;;    as published by the Free Software Foundation; either version
;;;;    2 of the License, or (at your option) any later version.
;;;;    
;;;;    This program is distributed in the hope that it will be
;;;;    useful, but WITHOUT ANY WARRANTY; without even the implied
;;;;    warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
;;;;    PURPOSE.  See the GNU General Public License for more details.
;;;;    
;;;;    You should have received a copy of the GNU General Public
;;;;    License along with this program; if not, write to the Free
;;;;    Software Foundation, Inc., 59 Temple Place, Suite 330,
;;;;    Boston, MA 02111-1307 USA
;;;;****************************************************************************

(IN-PACKAGE "COMMON-LISP-USER")
(DEFPACKAGE "COM.INFORMATIMAGO.COMMON-LISP.PEEK-STREAM"
  (:USE "COMMON-LISP")
  (:EXPORT "NEXTCHAR" "UNGETCHAR" "GETCHAR" "PEEK-STREAM")
  (:DOCUMENTATION
   "
    This package exports a class named PEEK-STREAM that encapsulates
    a stream and a buffer in such a way that reading, peeking or
    unreading characters can be done in any number and in any order.
    
    We don't use gray stream to keep it pure Common-Lisp.
    The I/O methods are GETCHAR, UNGETCHAR and NEXTCHAR to avoid
    name clashes with un-generic READ-CHAR, UNREAD-CHAR and PEEK-CHAR.

    Copyright Pascal J. Bourguignon 2004 - 2004
   
    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License
    as published by the Free Software Foundation; either version
    2 of the License, or (at your option) any later version.
    "))
(IN-PACKAGE "COM.INFORMATIMAGO.COMMON-LISP.PEEK-STREAM")


(DEFGENERIC EXTEND-BUFFER (SELF))
(DEFGENERIC GETCHAR (SELF))
(DEFGENERIC UNGETCHAR (SELF CH))
(DEFGENERIC NEXTCHAR (SELF))


(DEFCLASS PEEK-STREAM ()
  ((INSTRE :READER   INSTRE :INITARG :STREAM  :TYPE STREAM)
   (NEXT   :ACCESSOR NEXT   :INITFORM  8 :TYPE FIXNUM)
   (HEAD   :ACCESSOR HEAD   :INITFORM  8 :TYPE FIXNUM)
   (TAIL   :ACCESSOR TAIL   :INITFORM  8 :TYPE FIXNUM)
   (BUFFER :ACCESSOR BUFFER :INITFORM (MAKE-ARRAY '(16)
                                                  :ADJUSTABLE T
                                                  :ELEMENT-TYPE 'CHARACTER)))
  (:DOCUMENTATION "More than on character may be peeked and unread from this.")
  ) ;;PEEK-STREAM



;; | | | | | | |C|o|m|m|o|n|-|L| | | | |
;;              ^     ^         ^
;;              |     |         |
;;              |     |         +--- tail
;;              |     +------------- next
;;              +--------------------head


(DEFMETHOD PRINT-OBJECT ((SELF PEEK-STREAM) (STREAM STREAM))
  (FORMAT STREAM "#<PEEK-STREAM: \"窿堍ㄈ湖魏院末泳ㄉㄔ辽优唐ㄈ帕优唐┅ㄌ捎ㄓ章优抡破乓优唐ㄈ帕优唐┅ㄓ章优抡破乓优唐ㄔ辽优唐┅ㄌ捎ㄓ章优抡破乓优唐ㄈ帕优唐ㄔ辽优唐┅┅ㄈ帕优唐ㄎ咆优唐ㄔ辽优唐ㄉ斡砸优唐┅换幸晌原下逝迷呐仆撩蚁拖沫晌闷ㄍ夏仗刑撩ο性上瘟ㄉ蚊遗团卧暴抡呛碰犰踽翦刑撩箦鲥蜥糸礤螽啜优云刑撩ㄍ夏ǐ刑撩晌靡磐盼冤拖恼滔┅换拖沫晌闷呐仆撩蚁拖沫呐闷ㄍ夏仗刑撩ο性上瘟呐靡磐盼暴抡呛碰犰踽翦刑撩箦鲥蜥糸礤螽啜优云刑撩ㄍ夏ō刑撩呐靡磐盼冤拖恼滔┅换拖沫呐闷呐仆旁认咆耘文抡破乓è优唐信潘釉遗镣┅ㄌ旁è咸沫膛吻匀ㄌ盼窃抡破乓优唐┅┅聊收釉烈伊抡破乓优唐ㄌ捎í咸沫膛吻匀┅ㄒ判塘门抡破乓优唐抡破乓优唐河粤以咸沫膛吻匀河粤以号文ㄔ辽优唐┅ㄍ夏晌闷ㄌ盼窃抡破乓优唐┅ㄔ辽优唐咸沫膛吻匀┅换咆耘文抡破乓呐仆旁认桥悦攘è优唐信潘釉遗镣┅遗哉椅澡铄汨狎徙翦骝镯优唐ㄉ汜忮汨狎徙翦铄黛蝈徜骝镯翳孱汜痼蹯狒邃篝蝈犴矧汨狎徙翦怩骀弪邃怡闻卦萌烈矧瘴桥悦攘药ㄉㄈ帕优唐ㄔ辽优唐┅ㄒ帕沫萌烈ㄉ斡砸优唐紊紊泰ㄐ蚁潜烈牌抡破乓优唐ㄈ帕优唐┅ㄍ夏晌闷ㄌ盼窃抡破乓优唐┅ㄈ帕优唐┅ㄓ旁ㄎ咆优唐ㄈ帕优唐┅┅换桥悦攘呐仆旁认瘴桥悦攘è优唐信潘釉遗镣萌萌烈撩耘药南絮翳汨狎徙翦萌轭骝镱镦翳轭瘐怩骀弪婶滹弩铒铄邃麸忮翳筢礤狍犷汨狎徙翦蝈徜骝镯优唐ㄍ夏呐闷ㄌ盼窃抡破乓优唐┅ㄈ帕优唐┅ㄗ扰ㄈ帕优唐ㄔ辽优唐┅ㄍ夏晌闷ㄌ盼窃抡破乓优唐┅ㄈ帕优唐┅ㄅ卦盼沫抡破乓优唐ㄍ夏呐闷ㄌ盼窃抡破乓优唐┅ㄈ帕优唐┅ㄓ旁烈牌抡破乓优唐ㄈ帕优唐┅萌ㄎ咆优唐ㄈ帕优唐┅换瘴桥悦攘换躅珏翥栳浇ㄤ邈桢徜┈瘐汨狎狒ㄡ蝈怩骀弪桢徜┈铄艉借遽换珏翥栳浇珏汨狎狒ㄡ蝈怩骀弪桢徜┈ㄩ钽桢徜┈铄艉借遽换桢徜浇翎殪浇蝈徜汨狎换铄翥栳浇珏汨狎狒ㄡ蝈怩骀弪铄舂ㄩ钽铄舂换铄艚紧衢浇蝈徜汨狎换瘐汨狒ㄡ蝈怩骀弪翎殪┈换ㄩ钽翎殪ㄩ钽铄舂呐仆旁认闻卦萌烈è优唐信潘釉遗镣┅遗哉椅澡汨狎徙翦翳狒鏖祆忮蝈徜箫镱怡桥悦攘椰矧紊麒孱畔飘ㄥ聃犰祜镳蝈疱狒骘汨铄翥栳痼泔祆邈汨轭麸蝈篚祠骈钺祆蝈趱蝾蝈篚祠┅祜镳蝈疱狒骘汨ㄧ弭汨狎痼泔祆邈汨轭麸蝈篚祠骈钺祆蝈趱蝾蝈篚祠┅ㄉǒㄎ咆优唐ㄔ辽优唐┅ㄐ蚁潜烈牌抡破乓优唐ㄎ咆优唐┅ㄍ夏晌闷ㄌ盼窃抡破乓优唐┅ㄎ咆优唐┅ㄌ旁è萌ㄒ帕沫萌烈ㄉ斡砸优唐紊紊泰┅ㄗ扰萌ㄓ旁烈牌抡破乓优唐ㄔ辽优唐┅萌ㄍ夏晌闷ㄌ盼窃抡破乓优唐┅ㄔ辽优唐┅ㄗ扰ㄈ帕优唐ㄔ辽优唐┅ㄍ夏呐闷ㄌ盼窃抡破乓优唐┅ㄔ辽优唐┅ㄅ卦盼沫抡破乓优唐ㄍ夏晌闷ㄌ盼窃抡破乓优唐┅ㄔ辽优唐┅ㄓ旁ㄎ咆优唐ㄔ辽优唐┅萌┅换闻卦萌烈呐普耘釉ī南陨团ㄎ卑ㄗ稍拳晌姓原埔贤釉疑吻ㄉ⒚镯惋瞽涕箴ㄌ旁è杏ㄍ了怒晌釉廖门信潘釉遗镣河砸帕晌┅ㄎㄌ舷遗信猎葡萌ㄎ咆悦攘杏孟烫琶萌晌韵遗诱淘粕瘟烫ㄒ旁找遗诱淘┅ㄇㄌ舷遗信猎葡萌ㄇ旁萌烈杏孟烫琶萌晌韵遗诱淘粕瘟烫ㄒ旁找遗诱淘┅┅劣优以ㄅ颜撂蚊敲┅┅ㄗ稍拳晌姓原埔贤釉疑吻ㄉ⒚镯惋瞽涕箴ㄌ旁è杏ㄍ了怒晌釉廖门信潘釉遗镣河砸帕晌┅帽貌贸劣优以ㄅ颜撂ㄌ捎ㄇ旁萌烈杏ㄇ旁萌烈杏ㄇ旁萌烈杏┅Ж＼＼＼桐┅ㄓ旁帽ㄇ旁萌烈杏貌ㄇ旁萌烈杏贸ㄇ旁萌烈杏┅劣优以ㄅ颜撂ㄌ捎帽貌贸ㄎ咆悦攘杏┅Ж＼＼＼＼┅ㄕ吻旁萌烈杏贸┄瘴桥悦攘杏貌┄瘴桥悦攘杏帽劣优以ㄅ颜撂ㄌ捎ㄇ旁萌烈杏ㄇ旁萌烈杏ㄇ旁萌烈杏┅Ж＼＼＼惟┅劣优以ㄅ颜撂ㄌ捎ㄇ旁萌烈杏ㄇ旁萌烈杏ㄇ旁萌烈杏┅Ж＼＼＼嫂┅┅ㄗ稍拳晌姓原埔贤釉疑吻ㄉ⒚镯盹瞽涕箴ㄌ旁è杏ㄍ了怒晌釉廖门信潘釉遗镣河砸帕晌┅帽貌贸劣优以ㄅ颜撂ㄌ捎ㄇ旁萌烈杏ㄇ旁萌烈杏ㄇ旁萌烈杏┅Ж＼＼＼桐┅ㄓ旁帽ㄇ旁萌烈杏貌ㄇ旁萌烈杏┅劣优以ㄅ颜撂ㄌ捎帽貌ㄎ咆悦攘杏┅Ж＼＼＼惟┅ㄓ旁贸ㄇ旁萌烈杏┅劣优以ㄅ颜撂ㄌ捎贸ㄎ咆悦攘杏┅Ж＼＼┅ㄕ吻旁萌烈杏贸┄瘴桥悦攘杏貌┄瘴桥悦攘杏帽劣优以ㄅ颜撂ㄌ捎ㄇ旁萌烈杏ㄇ旁萌烈杏ㄇ旁萌烈杏┅Ж＼＼＼惟┅劣优以ㄅ颜撂ㄌ捎ㄇ旁萌烈杏ㄇ旁萌烈杏ㄇ旁萌烈杏┅Ж＼＼＼嫂┅┅ㄖ撂张萤换换疱咫篝蝈犴扉箴换换