;;;; -*- coding:utf-8 -*-
;;;;****************************************************************************
;;;;FILE:               peek-stream.lisp
;;;;LANGUAGE:           Common-Lisp
;;;;SYSTEM:             Common-Lisp
;;;;USER-INTERFACE:     NONE
;;;;DESCRIPTION
;;;;    
;;;;    This package exports a class named PEEK-STREAM that encapsulates
;;;;    a stream and a buffer in such a way that reading, peeking or
;;;;    unreading characters can be done in any number and in any order.
;;;;    
;;;;    We don't use gray stream to keep it pure Common-Lisp.
;;;;    The I/O methods are GETCHAR, UNGETCHAR and NEXTCHAR to avoid
;;;;    name clashes with un-generic READ-CHAR, UNREAD-CHAR and PEEK-CHAR.
;;;;    
;;;;AUTHORS
;;;;    <PJB> Pascal J. Bourguignon <pjb@informatimago.com>
;;;;MODIFICATIONS
;;;;    2012-02-07 <PJB> Corrected mod-incf and mod-decf.
;;;;    2004-09-06 <PJB> Extracted from parse-html.
;;;;BUGS
;;;;    Does not implement other I/O than these three character input methods.
;;;;    NEXTCHAR can be called only once in a row.
;;;;LEGAL
;;;;    AGPL3
;;;;    
;;;;    Copyright Pascal J. Bourguignon 2004 - 2012
;;;;    
;;;;    This program is free software: you can redistribute it and/or modify
;;;;    it under the terms of the GNU Affero General Public License as published by
;;;;    the Free Software Foundation, either version 3 of the License, or
;;;;    (at your option) any later version.
;;;;    
;;;;    This program is distributed in the hope that it will be useful,
;;;;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;;    GNU Affero General Public License for more details.
;;;;    
;;;;    You should have received a copy of the GNU Affero General Public License
;;;;    along with this program.  If not, see <http://www.gnu.org/licenses/>.
;;;;****************************************************************************

(in-package "COMMON-LISP-USER")
(defpackage "COM.INFORMATIMAGO.COMMON-LISP.CESARUM.PEEK-STREAM"
  (:use "COMMON-LISP")
  (:export "PEEK-STREAM" "NEXTCHAR" "UNGETCHAR" "GETCHAR" "READLINE")
  (:documentation
   "
    This package exports a class named PEEK-STREAM that encapsulates
    a stream and a buffer in such a way that reading, peeking or
    unreading characters can be done in any number and in any order.
    
    We don't use gray stream to keep it pure Common-Lisp.
    The I/O methods are GETCHAR, UNGETCHAR and NEXTCHAR to avoid
    name clashes with un-generic READ-CHAR, UNREAD-CHAR and PEEK-CHAR.

    Copyright Pascal J. Bourguignon 2004 - 2004
   
    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License
    as published by the Free Software Foundation; either version
    2 of the License, or (at your option) any later version.
    "))
(in-package "COM.INFORMATIMAGO.COMMON-LISP.CESARUM.PEEK-STREAM")


(defgeneric extend-buffer (self))
(defgeneric getchar (self))
(defgeneric ungetchar (self ch))
(defgeneric nextchar (self))
(defgeneric readline (self))


(defclass peek-stream ()
  ((instre :reader   instre :initarg :stream  :type stream)
   (next   :accessor next   :initform  8 :type fixnum)
   (head   :accessor head   :initform  8 :type fixnum)
   (tail   :accessor tail   :initform  8 :type fixnum)
   (buffer :accessor buffer :initform (make-array '(16)
                                                  :adjustable t
                                                  :element-type 'character)))
  (:documentation "More than on character may be peeked and unread from this."))



;; | | | | | | |C|o|m|m|o|n|-|L| | | | |
;;              ^     ^         ^
;;              |     |         |
;;              |     |         +--- tail
;;              |     +------------- next
;;              +--------------------head


(defmethod print-object ((self peek-stream) (stream stream))
  (format stream "#<PEEK-STREAM: \"窿堍ㄈ湖魏院末泳ㄩ翎殪箦戽ㄨ遽箦戽┅扉篝篚怏羼ㄢ蹑驽箦戽ㄨ遽箦戽┅篚怏羼ㄢ蹑驽箦戽翎殪箦戽┅扉篝篚怏羼ㄢ蹑驽箦戽ㄨ遽箦戽翎殪箦戽┅┅ㄨ遽箦戽铄箦戽翎殪箦戽ㄩ铙趄箦戽┅ㄤ彐磲泸盹洵轭沔盹漉祜痨徙镳糸镱犰ㄩ钽蝈礤铘暴孱鲩蝻铐孱孱雯⑸钽蝈礤铘翳刑撩怡晌靡磐盼盹漉祜拖恼滔眭祠轲戾鲠祯瀛忾钿翦眇鲠祗篝矧弩篝矧瀛骘蝽徙沐篌骘蝽ㄧ弭箦翩屮疳铙轱痨徙孱雯啜戾舄ì括磲疸狎ㄦ躅泗轱扉篝翦眇鲠祗ìㄦ轵篝篝矧弩盹ǐ徙沐篌骘蝽轭泸屙孱舂盹漉祜┅篝矧瀛骘蝽┅ㄤ彐磲泸盹洵溴沔盹漉祜痨徙镳糸镱犰ㄤ邈蝈礤铘暴孱鲩蝻铐孱孱雯⒛邈蝈礤铘翳刑撩怡呐靡磐盼盹漉祜拖恼滔眭祠轲戾鲠祯瀛忾钿翦眇鲠祗篝矧弩篝矧瀛骘蝽徙沐篌骘蝽ㄧ弭箦翩屮疳铙轱痨徙孱雯啜戾舄ì括磲疸狎ㄦ躅泗轱扉篝翦眇鲠祗ìㄦ轵篝篝矧弩盹ō徙沐篌骘蝽溴泸屙孱舂盹漉祜┅篝矧瀛骘蝽┅ㄤ彐礤翳镤屮翦钿怩骀弪è箦戽疱咫篝蝈犴┅戾è镬洵戾铉翳戾铉翳ㄢ蹑驽箦戽┅┅ㄡ潢躞舡狎蜥ㄢ蹑驽箦戽扉篝í镬洵戾铉翳┅蝈痨徙ㄢ蹑驽箦戽ㄢ蹑驽箦戽后翎螋镬洵戾铉翳后翎螋哄钿翎殪箦戽┅盹洵轭沔戾铉翳ㄢ蹑驽箦戽┅翎殪箦戽镬洵戾铉翳┅ㄤ彐礤翳镤珏翥栳è箦戽疱咫篝蝈犴┅遗哉椅澡铄汨狎徙翦骝镯优唐ㄉ汜忮汨狎徙翦铄黛蝈徜骝镯翳孱汜痼蹯狒邃篝蝈犴矧汨狎徙翦怩骀弪邃怡闻卦萌烈矧瘴桥悦攘药ㄩㄨ遽箦戽翎殪箦戽┅蝈徜汨狎ㄩ铙趄箦戽铋铋飑痱镧ㄡ蝈ㄢ蹑驽箦戽ㄨ遽箦戽┅盹洵轭沔戾铉翳ㄢ蹑驽箦戽┅ㄨ遽箦戽┅箦翩铄箦戽ㄨ遽箦戽┅┅ㄤ彐礤翳镤躅珏翥栳è箦戽疱咫篝蝈犴ㄣ铛祆┅汨ㄤ彐礤翳镤躅珏翥栳è箦戽疱咫篝蝈犴ㄣ汨狎徙翦颟南絮翳汨狎徙翦萌轭骝镱镦翳轭瘐怩骀弪婶滹弩铒铄邃麸忮翳筢礤狍犷汨狎徙翦蝈徜骝镯优唐遗哉椅萌盹洵溴沔戾铉翳ㄢ蹑驽箦戽┅ㄨ遽箦戽┅麒孱ㄨ遽箦戽翎殪箦戽┅盹洵轭沔戾铉翳ㄢ蹑驽箦戽┅ㄨ遽箦戽┅ㄥ翦钿怩骀弪箦戽盹洵溴沔戾铉翳ㄢ蹑驽箦戽┅ㄨ遽箦戽┅箦翩ㄡ蝈ㄢ蹑驽箦戽ㄨ遽箦戽┅汨铄箦戽ㄨ遽箦戽┅汨换躅珏翥栳浇ㄤ邈桢徜┈瘐汨狎狒ㄡ蝈怩骀弪桢徜┈铄艉借遽换珏翥栳浇珏汨狎狒ㄡ蝈怩骀弪桢徜┈ㄩ钽桢徜┈铄艉借遽换桢徜浇翎殪浇蝈徜汨狎换铄翥栳浇珏汨狎狒ㄡ蝈怩骀弪铄舂ㄩ钽铄舂换铄艚紧衢浇蝈徜汨狎换瘐汨狒ㄡ蝈怩骀弪翎殪┈换ㄩ钽翎殪ㄩ钽铄舂ㄤ彐礤翳镤铄翥栳è箦戽疱咫篝蝈犴┅遗哉椅澡汨狎徙翦翳狒鏖祆忮蝈徜箫镱怡桥悦攘椰矧紊麒孱畔飘ㄥ聃犰祜镳蝈疱狒泔祆邈铄翥栳痼┅祜镳蝈疱狒泔祆邈ㄧ弭汨狎痼┅ㄩǒ铄箦戽翎殪箦戽┅痱镧ㄡ蝈ㄢ蹑驽箦戽铄箦戽┅盹洵轭沔戾铉翳ㄢ蹑驽箦戽┅铄箦戽┅戾è汨蝈徜汨狎ㄩ铙趄箦戽铋铋飑┅麒孱汨箦翩ㄡ蝈ㄢ蹑驽箦戽翎殪箦戽┅汨盹洵轭沔戾铉翳ㄢ蹑驽箦戽┅翎殪箦戽┅麒孱ㄨ遽箦戽翎殪箦戽┅盹洵溴沔戾铉翳ㄢ蹑驽箦戽┅翎殪箦戽┅ㄥ翦钿怩骀弪箦戽盹洵轭沔戾铉翳ㄢ蹑驽箦戽┅翎殪箦戽┅箦翩铄箦戽翎殪箦戽┅汨┅ㄤ彐礤翳镤蝈徜扉铄è箦戽疱咫篝蝈犴┅遗哉椅麒镬扉铄蝈徜骝镯翳疱咫篝蝈犴矧紊轭汜箦镦孱镦篝蝈犴麒孱铄翥栳箦戽祜镳瑚轸扉铄磲脲狎蜥赴哄戾礤铘豉疱с栳蜥泗弪横潢躞翎忪烘殪飙痫轭翦癌烘矧汨ㄧ弭汨狎箦戽瑚栝戾汨轰ㄩㄣ栳蚪汨＼五黛轭濠祜镳骈铋箬鲥泗矧瘐箬屮翦钿汨扉铄戾铉翳扉铄┅烘轭犰禊蝈趱蝾扉铄┅┅ㄤ彐躅翦篝īㄤ雉轫弩卑鏖翳轭瘐舡骝镯篝蜷铉ㄩ⒚镯惋瞽涕箴戾舄è痼磲脲轭篝犷沐ю邋氕篝蝈犴后趄遽轭┅钽祜镳蝈疱狒骘汨铄翥栳痼泔祆邈汨轭麸蝈篚祠骈钺祆蝈趱蝾蝈篚祠┅ㄧ祜镳蝈疱狒骘汨ㄧ弭汨狎痼泔祆邈汨轭麸蝈篚祠骈钺祆蝈趱蝾蝈篚祠┅┅ㄡ篌弪ㄥ聃犰钽玢┅┅鏖翳轭瘐舡骝镯篝蜷铉ㄩ⒚镯惋瞽涕箴戾è痼磲脲轭篝犷沐ю邋氕篝蝈犴后趄遽轭┅惚悴愠ㄡ篌弪ㄥ聃犰扉篝ㄧ弭汨狎痼ㄧ弭汨狎痼ㄧ弭汨狎痼┅Ж＼＼＼愆┅箦翩惚ㄧ弭汨狎痼悴ㄧ弭汨狎痼愠ㄧ弭汨狎痼┅ㄡ篌弪ㄥ聃犰扉篝惚悴愠铄翥栳痼┅Ж＼＼＼＼┅躅珏翥栳痼愠┄躅珏翥栳痼悴┄躅珏翥栳痼惚ㄡ篌弪ㄥ聃犰扉篝ㄧ弭汨狎痼ㄧ弭汨狎痼ㄧ弭汨狎痼┅Ж＼＼＼瞟┅ㄡ篌弪ㄥ聃犰扉篝ㄧ弭汨狎痼ㄧ弭汨狎痼ㄧ弭汨狎痼┅Ж＼＼＼椹┅┅鏖翳轭瘐舡骝镯篝蜷铉ㄩ⒚镯盹瞽涕箴戾è痼磲脲轭篝犷沐ю邋氕篝蝈犴后趄遽轭┅惚悴愠ㄡ篌弪ㄥ聃犰扉篝ㄧ弭汨狎痼ㄧ弭汨狎痼ㄧ弭汨狎痼┅Ж＼＼＼愆┅箦翩惚ㄧ弭汨狎痼悴ㄧ弭汨狎痼┅ㄡ篌弪ㄥ聃犰扉篝惚悴铄翥栳痼┅Ж＼＼＼瞟┅箦翩愠ㄧ弭汨狎痼┅ㄡ篌弪ㄥ聃犰扉篝愠铄翥栳痼┅Ж＼＼┅躅珏翥栳痼愠┄躅珏翥栳痼悴┄躅珏翥栳痼惚ㄡ篌弪ㄥ聃犰扉篝ㄧ弭汨狎痼ㄧ弭汨狎痼ㄧ弭汨狎痼┅Ж＼＼＼瞟┅ㄡ篌弪ㄥ聃犰扉篝ㄧ弭汨狎痼ㄧ弭汨狎痼ㄧ弭汨狎痼┅Ж＼＼＼椹┅┅鲠祯弩┅换换匀盼换换