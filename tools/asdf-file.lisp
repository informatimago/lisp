;;;; -*- mode:lisp;coding:utf-8 -*-
;;;;**************************************************************************
;;;;FILE:               asdf-file.lisp
;;;;LANGUAGE:           Common-Lisp
;;;;SYSTEM:             Common-Lisp
;;;;USER-INTERFACE:     NONE
;;;;DESCRIPTION
;;;;    
;;;;    Reads ASDF files.
;;;;    
;;;;AUTHORS
;;;;    <PJB> Pascal J. Bourguignon <pjb@informatimago.com>
;;;;MODIFICATIONS
;;;;    2014-09-02 <PJB> Added generate-dot.
;;;;    2013-09-06 <PJB> Updated for publication.
;;;;    2012-04-09 <PJB> Created.
;;;;BUGS
;;;;LEGAL
;;;;    AGPL3
;;;;    
;;;;    Copyright Pascal J. Bourguignon 2012 - 2015
;;;;    
;;;;    This program is free software: you can redistribute it and/or modify
;;;;    it under the terms of the GNU Affero General Public License as published by
;;;;    the Free Software Foundation, either version 3 of the License, or
;;;;    (at your option) any later version.
;;;;    
;;;;    This program is distributed in the hope that it will be useful,
;;;;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;;    GNU Affero General Public License for more details.
;;;;    
;;;;    You should have received a copy of the GNU Affero General Public License
;;;;    along with this program.  If not, see <http://www.gnu.org/licenses/>.
;;;;**************************************************************************

(eval-when (:compile-toplevel :load-toplevel :execute)
  (unless (find-package "ASDF")
    (defpackage "ASDF" (:use "COMMON-LISP") (:export "DEFSYSTEM")))
  (unless (find-package "UIOP")
    (defpackage "UIOP" (:use "COMMON-LISP") (:export "SYMBOL-CALL"))))


(defpackage "COM.INFORMATIMAGO.TOOLS.ASDF-FILE"
  (:use "COMMON-LISP"
        "SPLIT-SEQUENCE"
        "COM.INFORMATIMAGO.COMMON-LISP.CESARUM.UTILITY"
        "COM.INFORMATIMAGO.COMMON-LISP.CESARUM.LIST"
        "COM.INFORMATIMAGO.COMMON-LISP.CESARUM.STRING"
        "COM.INFORMATIMAGO.TOOLS.DEPENDENCY-CYCLES"
        "COM.INFORMATIMAGO.TOOLS.SOURCE")
  (:shadow "DEPENDENCIES")
  (:export
   ;; Generating simple ASD files:
   "MAKE-COMPONENTS" 
   "GENERATE-ASD"
   ;; Reading and writing asd files:
   "READ-ASDF-SYSTEM-DEFINITIONS"
   "WRITE-ASDF-SYSTEM-DEFINITION"
   "SAVE-ASDF-SYSTEM-FILE"
   "DEFAULT-HEADERS-FOR-SYSTEM"
   ;; Generating test systems:
   "TEST-SYSTEM-FOR-SYSTEM"
   "TEST-SYSTEM-P"
   "TEST-SYSTEM-FOR-SYSTEM"
   "GENERATE-TEST-SYSTEM-FOR-SYSTEM-AT-PATH")
  (:documentation "

Reads simple .asd files, without instanciating ASDF objects.
============================================================

  (LOAD-SIMPLE-ASD-FILE path-to-asd-file)
  --> hashtable mapping file names to ASDF-FILE structures.

NOTE: The current implementation expects the defsystem form to be the
      first and only form in the asd file.


Generate simple .asd files:
============================================================

  (GENERATE-ASD :system-name (list \"source-1\" \"source-2\") \"lisp\"
                :description \"Short description\"
                :version \"1.0.0\"
                :author \"Name <email@address>\"
                :license \"AGPL3\"
                :predefined-packages '(\"COMMON-LISP\"))
                :implicit-dependencies '())
                :depends-on '(:other-system))
                :load-paths (list (make-pathname :directory '(:relative))))
                :vanillap t)

License:

    AGPL3
    
    Copyright Pascal J. Bourguignon 2012 - 2014
    
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.
    
    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.
    
    You should have received a copy of the GNU Affero General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
"))
(in-package "COM.INFORMATIMAGO.TOOLS.ASDF-FILE")



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GENERATE-ASD
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


(defun make-components (paths &key (predefined-packages '("COMMON-LISP"))
                                (component-class :file)
                                (implicit-dependencies '())
                                (load-paths (list (make-pathname
                                                   :directory '(:relative)))))
  (mapcar
   (lambda (depend)
     (let* ((depend (mapcar (lambda (path) (pathname-name path)) depend))
            (target (first depend))
            (depends  (delete (first depend)
                              (append implicit-dependencies (rest depend))
                              :test (function string=))))
       (list* component-class target (when depends (list :depends-on depends)))))
   (get-depends paths predefined-packages load-paths)))


(defun gen-defsystem-form (name paths &key  description (version "0.0.0") 
                                        author maintainer licence license
                                        (component-class :file)
                                        (predefined-packages '("COMMON-LISP"))
                                        (implicit-dependencies '())
                                        (depends-on '())
                                        (load-paths (list (make-pathname
                                                           :directory '(:relative)))))
  "
DO:             Generate an ASD file for ASDF.
NAME:           Name of the generated ASDF system.
PATHS:          List of pathnames to the source files of this ASDF system.
DESCRIPTION:    A description string for the ASDF system.
VERSION:        A version string for the ASDF system.
AUTHOR:         An author string for the ASDF system.
LICENSE:        A licence string for the ASDF system.
PREDEFINED-PACKAGES:   A list of packages that are removed from the dependencies.
IMPLICIT-DEPENDENCIES: A list of dependencies added to all targets.
LOAD:-PATHS     A list of directory paths where the sources are searched in.
"
  (flet ((enumerate (list) (format nil "连:[none;1@*窿犷窿茛ㄢ豸灬篝扉篝博灬篝扉篝博┅戾舄è桢徜弪磲疸狎灬礅溽疳翳扉篝吼狒疳翳鏖翳镳孱骈戾篝蝈犴疳翳蝈徜箫躜沐桢徜弪篝蝈犴┅┅疳翳螬ㄡ豸栾蝮矧狨翳矧ㄥ铛礤蜥翦ㄤ屐弭瀛漉痨殂狒弩ㄡ痧禊ㄦ躅泗轱狃疱钿磲疸狎ㄦ躅泗轱桢徜弪狨翳矧螬桢徜弪螬呼弩ㄦ躅泗轱篝蜷铉羼踽飑┅┅扉沐钽矧扉沐钽扉沐铙ㄥ铛礤蜥翦ㄤ屐弭瀛漉痨殂狒弩磲疸狎ㄦ躅泗轱桢徜弪扉沐钽濠桢徜弪螬呼弩ㄦ躅泗轱篝蜷铉羼踽飑┅┅ㄤ弩泸轲糸镱躅箴扉舡篝蜷铉矧ㄥ铙躜瀛扉篝溴筱蜷痿轱瞟磲疸犷灬礅溽ㄨ遽溴颟ㄡ痧孱扉篝ㄦ矧磲铋播辛盟燎藕窿播箦泔钿ㄧ弭疳汶徵ㄨ遽溴颦箪雉桢徜弪吼狒瑭┅┅磲疸狎灬礅溽扉铄ㄦ矧磲铋窿ア扉铄┅ㄨ遽溴颦溴筱蜷痿轱桢徜弪┅扉篝ㄦ矧磲铋ア┅┅桢徜弪螬┅ㄣ镯痫铄铘磲脲泔眇镱孱趔疳翳恒镯痫铄铘沆狍泔眇镱孱舡沆狍吼蝈溴骈铄洵疳汶徵弩ㄡ痧孱溴疱钿蟓镱痱邃彐轭邃疳汶徵弩洪眇扉汩舡溴疱钿孱汩弩轫痨殂轸溴疱钿孱汩弩红镝洵疳翳祜徜疳翳螬┅啜狍滏轰彐簌篝屙钺礤轰弩泸轲糸镱溴筱蜷痿轱忽弪箝镱鲥蝮轱横豸栾狨翳矧喉衢铘衢铄磲轭翎轭弪红殂孱沐扉沐钽轰屦孱潴镱溴疱钿蟓镱恒镯痫铄铘泔眇镱孱趔┅┅ㄤ彐躅珏铄蜥翦狍簌篝屙钺礤箫躜沐箫躜沐豉疱脲溴筱蜷痿轱鲥蝮轱爱爱阿狨翳矧扉沐钽扉沐铙痱邃彐轭邃疳汶徵弩Ж⒚贤拖苇躺有┅ㄩ眇扉汩舡溴疱钿孱汩弩Ж┅ㄤ屦孱潴镱Ж┅祜徜疳翳扉篝磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹濠┅鲠铋祆狃舂至紊烫列殒趄蹂翳孱珏铄蜥翦箝眇戾鲠铋祆簌篝屙萧桢蝼轶瀣溴泔蜥翦轸鏖翳惺秕麴豸骈戾螽戾è疳汶徵濯ㄦ轭洵疳汶徵恒镯轭骘蝽狒轫徵锂麸镬螽磲脲溴疱钿螽磲脲溴疱钿螬┅鏖翳镳孱骈戾秕磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹濠侯犴Ⅲ篝屙换篝蜷铉滹黝汜箦簌篝屙钺礤呼疱⑨箐忽弪箝镱铋飑轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴洪姝滹弩铒舡屮轶恒蝈狒濠＋矧┄瘐箬趄蹂钺礤礤蜱瀛疳翳钺礤磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹濠侯犴铋呼疱铋忽弪箝镱铋飑秕舂狍滏汉沐铘蜥飙蝈玳篝蝙ㄦ矧磲秕⒒盹溴红轶ア磲疸灬礅溽箦皓痱轭箦秕舂翦蝠蜷秕舂换硝麸翳狍骈戾ㄡ痧孱躅戾篌鲠铋祆狃啜ㄤ彐疳汶徵⒚贤晌葡彝猎赏燎袭劣钠ê躞⒚贤拖苇躺有┅ㄩ瞽疳汶徵⒚贤晌葡彝猎赏燎袭劣钠换劣钠轫痫箦翳骈戾豉疱沆狍箦麸忮换轭翳筢礤疳汶徵狍翳溴骟篝屙躅戾篌ㄨ犷潇弪汜箦ㄦ轭洵沆狍ю赈沆箫躜沐骈戾ī铋飑ㄤ彐沆狍痍猸沆箫躜沐骈戾ㄡ箐婧恒飙箫躜沐骈戾īㄦ戾è秕麴豸骈戾ㄣㄦ戾è轫痨屙孱翎糸镱殇īㄦ戾è骈蝮舡黠蜾翦舂戾è痫痫箝糸镱ㄣ栳蜥泗弪翦舂┅蝈盹鲥ㄣ栳蜥泗弪ㄩ痫篚怏羼翦痫螬翦舂┅┅ㄦ矧磲铋镰镰立ㄣ镱è篝蜷铉羼踽⑸铘弪钺糸镱犰领戾珧锰蓬翦蝠蜷箦配轸轱睥扉箴轫痨屙孱翎糸镱豉疱┅⒘锰ㄦ轵篝黠蜾扉箴轫痨屙孱翎糸镱豉疱┅┅ㄦ轵篝黠蜾扉箴轫痨屙孱翎糸镱鲥蝮轱瞟ㄦ轵篝黠蜾磲汨轭瀛豉疱┅┅┅戾舄è镡赍泗ㄣ镯痖戾骈戾疳翳钺礤ㄡ箐婧恒镯痫铄铘疳翳钺礤悌┅疳翳礤蜱瀛疳翳钺礤磲脲疳翳钺礤轰轵邈麸蝙扉篝候屐狒轹ㄦ矧磲铋⑾率豪窿ㄩ眇戾礤铘狒轱瞽殇┅侯犴疳翳钺礤钺礤镡赍泗呼疱疳翳钺礤豉疱镡赍泗┅镡赍泗┅ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶疳翳扉篝疳翳┅┅ㄤ彐礤翳镤狍滏汉秕麴豸骈戾è镳弪狒轱狍滏汉泔眇殪瀛镳ㄣ痍猸沆箫躜沐骈戾┅秕麴豸骈戾悌ㄤ彐礤翳镤狍滏汉秕麴豸骈戾è镳弪狒轱狍滏汉祜徜镳ㄣ痍猸沆箫躜沐骈戾┅秕麴豸骈戾悌┅┅啜ㄧ孱溴骟篝屙骘蝽簌篝屙钺礤磲疸狎灬礅溽箫躜沐磲脲疳翳钺礤侯犴篝蜷铉滹黝汜箦篝蜷铉箫躜沐┅呼疱箫躜沐豉疱┅箫躜沐螬轰弩泸轲糸镱矧溴筱蜷痿轱ㄦ矧磲铋⒃栝劣钠簌篝屙玑翳弪犰翳疳汶徵弩篝蜷铉躔汜箦簌篝屙钺礤┅忽弪箝镱鲥蝮轱横豸栾狨翳矧喉衢铘衢铄狨翳矧红殂孱沐矧扉沐钽扉沐铙濠恒镯痫铄铘沆狍ㄩ鲠铋祆狃恒飙箫躜沐骈戾吼赈沆箫躜沐骈戾吼蝈溴骈铄洵疳汶徵弩痱邃彐轭邃疳汶徵弩洪眇扉汩舡溴疱钿孱汩弩轫痨殂轸溴疱钿孱汩弩轰屦孱潴镱溴疱钿蟓镱红镝洵疳翳祜徜疳翳螬┅┅┅换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换义徜轭犷黩轸轭劣骈戾狍箦痼换ㄤ彐躅蝈徜狍滏簌篝屙溴骈铋糸镱篝蝈犴义徜犷劣骈戾篝蝈犴犷蝈趱蝾扉篝镦狍滏轰彐簌篝屙骘蝽骘躅洚呐菩撩肆桥犷晌辛盟燎骘蝽狎弼犰踽翦洚戾è骘蝽蝈徜箫躜沐泔溴篝蝈犴呼弩灬礅溽箦皓ㄡ钿ㄣ镱箴箦皓ㄥ耢ㄦ轵篝箦皓п箐婧溴骟篝屙┅┅┅ㄣ潋ㄡ篌镢呼弩骘蝽螬┅ㄤ彐躅黩轸瀛狍滏簌篝屙溴骈铋糸镱篝蝈犴溴骟篝屙骘蝽⒆蜷翦翳溴骟篝屙骘蝽麸翳釉遗镣痫溴骟篝屙骘蝽鏖翳篝犷溽蜾轱簌铘狲戾è钺礤痫溴骟篝屙骘蝽┅ㄤ弩泸轲糸镱ㄧ弭溴骟篝屙骘蝽轰弩泸轲糸镱┅ㄡ豸栾ㄧ弭溴骟篝屙骘蝽横豸栾颟磲轭翎轭弪ㄧ弭溴骟篝屙骘蝽喉衢铘衢铄颟扉沐钽矧ㄧ弭溴骟篝屙骘蝽红殂孱箦ㄧ弭溴骟篝屙骘蝽红殂孱沐┅鲥蝮轱矧ㄧ弭溴骟篝屙骘蝽忽弪箝镱⒈爱阿┅痱镳弪糸弩ㄧ弭溴骟篝屙骘蝽吼蝻疱螋殄螬ㄥ钽镤轭ㄧ弭溴骟篝屙骘蝽哄钽镤轭绌ㄤ屦孱潴镱ㄧ弭溴骟篝屙骘蝽轰屦孱潴镱┅疱蜴矧ㄧ弭溴骟篝屙骘蝽吼弪骘蝽┅ㄩ瞽矧溴颦麸ㄧ弭溴骟篝屙骘蝽洪瞽矧溴颦麸┅ㄣ镯痫铄铘ㄧ弭溴骟篝屙骘蝽恒镯痫铄铘螬í痱轭舡痱弭豉舂í痱轭舡汜箦轰秣钽狍濠ㄦ矧磲篝蝈犴Θ狍滏轰彐簌篝屙英钺礤ㄦ矧磲篝蝈犴换簌篝屙狒趄殁豸弩孩ㄦ矧磲篝蝈犴钡英轰弩泸轲糸镱溴筱蜷痿轱瞟ㄦ矧磲篝蝈犴钡英横豸栾狨翳矧ㄦ矧磲篝蝈犴钡英喉衢铘衢铄矧狨翳矧磲轭翎轭弪┅ㄦ矧磲篝蝈犴钡英红殂孱沐扉沐钽濠ㄦ矧磲篝蝈犴换泔眇镱孱狒趄殁豸弩孩ㄦ矧磲篝蝈犴钡英忽弪箝镱鲥蝮轱瞟ㄦ矧磲篝蝈犴钡英吼蝻疱螋殄痱镳弪糸弩麒孱孱泔溟铉ㄦ矧磲篝蝈犴＋狍滏躅殂镤＋狍滏躅殂镤英哄钽镤轭孱泔溟铉┅ㄦ矧磲篝蝈犴钡誉撖惫件峻轰屦孱潴镱溴疱钿蟓镱ㄦ矧磲篝蝈犴钡誉撖惫件峻恒镯痫铄铘泔眇镱孱趔麒孱疱蜴矧ㄦ矧磲篝蝈犴钡誉撖惫件峻吼弪骘蝽疱蜴矧愆麒孱轭矧溴颦麸ㄦ矧磲篝蝈犴钡誉撖惫件峻洪瞽矧溴颦麸轭矧溴颦麸┅ㄦ矧磲篝蝈犴ア┅溴骟篝屙骘蝽ㄤ彐躅轭轸獒祗钺礤ㄣ镥蜚祜镳烘矧黠蜾洪箴扉舡箦聃孱沐＼箴徙钺礤候屙秭瀛屙痿篚怏羼舂瑚栝戾ㄡ祓栳汨狎ㄡ蝈黠蜾癌恒镬戾泗ㄡ蝈黠蜾癌篝蜷铉┅ㄤ彐躅溴驷蹯舡桢徜弪蟓骘颦簌篝屙疳翳钺礤溴骟篝屙骘蝽脲ㄤ彐狨祠狨翳矧⑿狍汜十嘛躜珲殓铒睥ㄤ彐狨祠屙衢痍饫轭骘蝽狒轫徵锂泔恝ㄤ彐狨祠轭轸獒祗⑿事┅遗哉椅瓠扉篝泔铘衢铋铉溴驷蹯箫躜沐骈戾桢徜弪骘翳骈戾狒辛匀瘟团泔铘衢铋铉翳呐朴儆耘铜葡彝ㄦ戾è蝈脲ㄣ狍脲ê钺礤箦泔钿溴骟篝屙骘蝽┅雉桢蝼轶ㄧ弭ㄣ滗溴骟篝屙骘蝽脲┅┅眭祠轲戾鲠祯瀛忾钿箦黹栾溽盹濠ㄤ邈镤瀛躅轹弪筢飙糸礤ㄧ弭躅轹弪筢飙糸礤┅ㄤ邈灬蝈ㄩ珙矧箦黹栾┅扉篝烘殪ㄦ殪瀛钺礤篝蜷铉疳翳钺礤红犷珲徵⒚镯盹瞽涕箴后篝屙⑽镱澧乎箦颦轭翦蜴徙⑽镱澧轰弩泸轲糸镱ㄡ痧孱扉篝ㄦ矧磲铋⒃栝骈戾溴骈铄翳簌篝屙蝈侯犴濠┅ㄥ铙躜瀛扉篝蝈轰弩泸轲糸镱┅ㄡ钿蝈红镱绛溴筱蜷痿轱瞟箴扉舡箦聃孱沐＼五黛轭蝈红镱绛溴筱蜷痿轱瞟┅乎筢珏Ж横豸栾蝮ㄦ戾è徜洵轭轸獒祗钺礤ㄦ矧磲铋⒓辆立ㄩ铋糸犰钺礤钺礤┅ㄩ蝈横豸栾颟磲疸狎ㄦ躅泗轱徜洵轭轸獒祗ㄩ蝈喉衢铘衢铄颟ㄩ篝蜷铉羼踽蝈横豸栾颟蝈喉衢铘衢铄颟ㄥ铙躜瀛扉篝蝈横豸栾颟扉篝蝈横豸栾颟蝈喉衢铘衢铄颟┅ㄥ铙躜瀛扉篝蝈横豸栾颟┅ㄩ蝈喉衢铘衢铄颟磲疸狎ㄦ躅泗轱徜洵轭轸獒祗ㄥ铙躜瀛扉篝蝈喉衢铘衢铄颟┅扉篝ㄦ矧磲铋⒓辆件辆溴驷蹯舡轭轸獒祗溴驷蹯舡狨翳矧溴驷蹯舡屙衢飑┅┅喉镤殒殂狒轱铙扉篝ㄦ矧磲铋船О沫铂О沫铂О件辆抿遽翦洚盹溽溴驷蹯舡轭轸獒祗┅衡蹒Ж红彗犰扉篝⒘切坛ㄦ矧磲铋⒚镳蜷玷邯立溴驷蹯舡狨翳矧濠⒃栝痱镧蜥轶骝邋箫骠麽蝈秕汜蝈溟篝蜷怩翦轸犷浏矧盹溟纟㈤躅溴翳翦蝽镦翳俏伶驽蝻清铄蜥絮忪殂涕沐铙狍瘐忪轶桢怡Ⅳ桢乞邋语骠麽蝈骑躅溽糸镱彘翳弪鲥蝮轱镦翳涕沐铙瀣矧ㄡ秕镳糸镱犷灬翦鲥蝮轱町⒃栝痱镧蜥轶溟篝蜷怩翦轭翳栾疱翳狒轸鏖祆忮躞彐蹯⑩豸咨匀险廖琢乙廖再鏖翳秕弼孱翳轫痨殄麽蝌犷豉镦⑼乓萌廖粤律躺再矧粕晕庞葡辛以擅仗烈姓倚嫌女渝翳澧⑶握伶驽蝻清铄蜥絮忪殂涕沐铙骘盹蝈溴翎殪螽①秕箬秕熹栳鲥蝈沐轹邃泔瘗镦翳俏伶驽蝻清铄蜥絮忪殂涕沐铙澧⑨祜铉鏖翳翳轶痱镧蜥懋涉铒衄箦艰趑鸷鼢鳟珙醍矧绡扉沐铙弩劲┅┅ㄤ彐躅筢鲥狍滏簌篝屙骈戾疳翳钺礤溴骟篝屙骘蝽脲ㄥ翦蝾犰骘蝽狒乎翩俯ㄥ磲泱桢徜鲠蜷徕戾Жê盹溴㈧轶稷ê泔溟铉Ⅴ翩涪┅ㄨ遽溴蝮Ж┅⒂狯弩翳呐朴儆耘铜葡彝轭麸翳劣骈戾狒辛匀瘟团篚疱蝮邃邃┊鏖翳镳孱骈戾篝蝈犴疳翳钺礤轰轵邈糸镱猴豸瘐洪姝滹弩铒舡屮轶恒蝈狒洪姝屮轶趔后躔弪箦溴哄翦蝾犰骘蝽狒屮翦蝾犰骘蝽狒黩轸瀛屙徙蟓桢徜鲠蜷徕戾屙徙蟓桢徜鲠蜷徕戾篝蝈犴黩轸瀛箫躜沐桢徜弪矧桢徜弪ㄤ彐狨祠桢徜弪蟓骘颦簌篝屙疳翳钺礤溴骟篝屙骘蝽┅篝蝈犴翦蝠蜷篝蝈犴黩轸瀛狍滏簌篝屙溴骈铋糸镱篝蝈犴溴骟篝屙骘蝽ㄦ矧磲篝蝈犴セ换匀盼换换ア┅换ㄤ彐躅翦篝簌篝屙狍洵骈戾桢徜弪疳翳钺礤溴姝翦篝邃簌篝屙换⒁弭躜铙桢徜弪骘颌换ㄤ彐狨祠桢徜弪蟓骘颦簌篝屙换ㄦ殪瀛钺礤篝蜷铉疳翳钺礤换扉篝п箐婧溴骟篝屙箦泔钿翦篝邃簌篝屙换轰弩泸轲糸镱扉篝⒃栝骈戾溴骈铄簌篝屙麸翦篝翳簌篝屙换篝蜷铉翦篝邃簌篝屙┅换ㄣ滗翦篝邃簌篝屙┅┅ㄤ彐躅翦篝簌篝屙ㄤ彐簌篝屙骘蝽⑿蝈溟汜翦麒弭桢呐朴儆耘铜葡彝溴骈铄翦篝簌篝屙殄麒弭桢翳簌篝屙钺礤孱潴轭堍翦篝堍戾è钺礤篝蜷铉箦泔钿溴骟篝屙骘蝽┅┅篚骀轼翦篝钺礤呼弩ㄦ躅泗轱汨狎羼踽飑┅ㄤ彐躅翦篝簌篝屙骘颦簌篝屙ㄤ彐簌篝屙骘蝽遗哉椅溴骟篝屙骘蝽骘翦篝簌篝屙骘翳簌篝屙溴骈铄怡呐朴儆耘铜葡彝ㄦ戾è蝈脲ㄣ狍脲ê钺礤篝蜷铉滹黝汜箦箦泔钿溴骟篝屙骘蝽┅雉桢蝼轶ㄧ弭ㄣ滗溴骟篝屙骘蝽脲┅┅眭祠轲戾鲠祯瀛忾钿箦黹栾溽盹濠ㄤ邈镤瀛躅轹弪筢飙糸礤ㄧ弭躅轹弪筢飙糸礤┅ㄤ邈灬蝈ㄩ珙矧箦黹栾溽┅戾舄è狨翳矧屙衢痍饫轭骘蝽狒轫徵锂泔恝ㄤ狒ㄦ矧磲铋圩轭翦蟒挥痱轭琬挥蹴礤蟒涣豸镯铪蘑趄躅汜翦盹畅濠翦篝邃簌篝屙钺礤蝈侯犴濠翦篝簌篝屙钺礤ㄦ矧磲铋廉翦篝翦篝邃簌篝屙钺礤┅秕麴豸溟蝈泗矧ㄦ矧磲铋繇鸠滹沲礤铘狒轱畀怜翦篝簌篝屙钺礤┅啜狍滏轰彐簌篝屙翦篝簌篝屙钺礤换簌篝屙狒趄殁豸弩轰弩泸轲糸镱ㄦ矧磲铋⒃弩趔翳簌篝屙翦篝邃簌篝屙钺礤红镱绛溴筱蜷痿轱矧蝈红镱绛溴泸轲糸镱蝈轰邈蜷痿轱瞟横豸栾蝈横豸栾颟喉衢铘衢铄蝈喉衢铘衢铄颟红殂孱沐矧蝈红殂孱沐蝈红殂孱箦┅换泔眇镱孱狒趄殁豸弩忽弪箝镱⒈爱阿蝈忽弪箝镱吼蝻疱螋殄è：狨翳矧屙衢狨翳矧屙衢飑ǎ轰狒溽翦è：犰忮螋：秕麴豸溟颟秕麴豸溟蝈泗矧è：犰忮螋：骘蝽狒螬á滹汊镲擘┅è：犰忮螋：滹汊镲：翦眇灬翦⑩镲擘è：犰忮螋：滹汊镲：忡泔祜颟Ⅶ栝翦è：犰忮螋：滹汊镲：翦翥镬矧⑩灬汶┅哄钽镤轭乎翩轰屦孱潴镱ì蝈侯犴濠泔懋轭骘蝽狒轫徵锂泔眄镱扉箴沐筢蝓恝箝眇戾翦篝吼弪骘蝽ㄡ箐婧翦篝镳ㄣ飙躞弪汉镳弪狒轱沆躞弪汉簌篝屙ㄤ邈灬蝈ㄩ珙矧沆躞弪汉镳弪狒轱沆躞弪汉簌篝屙┅换翦眇灬翦戾è疳汶徵濯ㄦ轭洵疳汶徵⒃庞耘沫辛盟燎泞┅蹰镳后礅镬汜祆⒃庞耘沫辛盟燎泞⒃庞辕撂挞┅恒镯痫铄铘è烘殪Ⅲ秕蜚瀛翦篝轰屦孱潴镱ī┅┅┅ㄤ彐躅珏铄蜥翦翦篝簌篝屙骘颦簌篝屙狒疳翳ㄡ箐姝簌篝屙疳翳钺礤脲鲥蜮矬舂昨轸弩狍骈戾溴骈铋铉翦篝簌篝屙骘遽汨簌篝屙骘躅轭翳狍滏骈戾狒劣钠淤釉磐辛匀瘟团躅戾篌篚汨骈戾犰蝈徜屮轶趔鏖翳镳孱骈戾篝蝈犴狍滏簌篝屙疳翳钺礤麒孱鲥蜮矬ㄦ矧磲趄徙瀛秕麴豸换义徜轭簌篝屙狍骈戾窿ア狍滏簌篝屙疳翳钺礤┅ㄤ镬轶ㄤ彐簌蝈徜狍滏簌篝屙溴骈铋糸镱篝蝈犴┅ㄩ翦篝簌篝屙溴骟螬麒孱鲥蜮矬ㄦ矧磲趄徙瀛秕麴豸换领蝈徜翦篝簌篝屙ア┅戾舄è翦篝溴骟翦篝簌篝屙骘颦簌篝屙溴骟螬翦篝疳翳钺礤礤蜱瀛疳翳钺礤磲脲疳翳钺礤侯犴篝蜷铉滹黝汜箦箦泔钿翦篝溴骟螬呼疱⑨箐忽弪箝镱铋恒狍红镢犰狍滏簌篝屙疳翳钺礤铋飑┅ㄩ痱镡瀛骈戾翦篝疳翳钺礤麒孱鲥蜮矬ㄦ矧磲趄徙瀛秕麴豸换藻篝簌篝屙骈戾犰蝈徜屮轶趔ア翦篝疳翳钺礤┅痱镧麒孱鲥蜮矬ㄦ矧磲趄徙瀛秕麴豸换清铄蜥糸铉翦篝簌篝屙狍骈戾窿ア翦篝疳翳钺礤┅筢鲥狍滏簌篝屙骈戾翦篝疳翳钺礤翦篝溴骟鸿遽溴蝮ㄤ彐狨祠桢徜弪蟓骘颦簌篝屙翦篝疳翳钺礤翦篝溴骟螬┅┅┅┅－ㄡ钿痱镧ㄤ彐狨祠桢徜弪蟓骘颦簌篝屙繇鸠岙扉箴Ж狍滏轰彐簌篝屙泔懋轭骘蝽狒轫徵锂泔眄镱扉箴沐筢蝓懋翦篝换簌篝屙狒趄殁豸弩轰弩泸轲糸镱⒃弩趔翳沐筢蝓扉怛狎横豸栾⑿狍汜十嘛躜珲殓铒拣赈篱铈矧磲糸磲顼泔砭喉衢铘衢铄⑿狍汜十嘛躜珲殓铒拣赈篱铈矧磲糸磲顼泔砭红殂孱沐⒘切坛换泔眇镱孱狒趄殁豸弩忽弪箝镱⒈钞尝吼蝻疱螋殄è：狨翳矧屙衢痍饫轭骘蝽狒轫徵锂泔恝ǎ轰狒⒆轭翦舶钡è：犰忮螋：秕麴豸溟颟繇鸠滹沲礤铘狒轱畀泔懋轭骘蝽狒轫徵锂泔眄镱扉箴沐筢蝓憝翦篝è：犰忮螋：骘蝽狒螬á滹汊镲擘┅è：犰忮螋：滹汊镲：翦眇灬翦⑩镲擘è：犰忮螋：滹汊镲：忡泔祜颟Ⅶ栝翦è：犰忮螋：滹汊镲：翦翥镬矧⑩灬汶┅＋狍滏躅殂镤哄钽镤轭＋狍滏躅殂镤乎翩轰屦孱潴镱á泔懋轭骘蝽狒轫徵锂泔眄镱扉箴沐筢蝓恝吼弪骘蝽ㄡ箐婧翦篝镳ㄣ飙躞弪汉沆躞弪汉螬戾è疳汶徵濯ㄦ轭洵疳汶徵⒚贤晌葡彝猎赏燎袭孟屯衔躺有门恿艺彤优寓┅蹰镳后礅镬汜祆⒚贤晌葡彝猎赏燎袭孟屯衔躺有门恿艺彤优寓⒃庞辕撂挞┅戾è疳汶徵濯ㄦ轭洵疳汶徵⒚贤晌葡彝猎赏燎袭孟屯衔躺有门恿艺彤晌呐丨优寓┅蹰镳后礅镬汜祆⒚贤晌葡彝猎赏燎袭孟屯衔躺有门恿艺彤晌呐丨优寓⒃庞辕撂挞┅恒镯痫铄铘è烘殪Ⅲ弭翦篝轰屦孱潴镱īê骈戾㈤钿屮箦舡翦篝轰屦孱潴镱á箦舡翦篝┅┅磲铋ㄦ躅泗轱珏铄蜥翦翦篝簌篝屙骘颦簌篝屙狒疳翳ㄤ轵邈麸蝙Ｐ篁惘瘐忪殂扉箴狍洧┅┗火蝻珙换换匀盼换换